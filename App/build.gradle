
buildscript {
    dependencies {
        classpath "com.android.tools.build:gradle:1.0.0"
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:2.1.1'
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.2"
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.1.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {

    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile 'proguard-project.txt'
        }
        debug {
            minifyEnabled false
            testCoverageEnabled = true
        }
    }

    productFlavors {
        normal {

        }
        integration {
        }
    }

    sourceSets.main {
        // src/gen is the target for generated content like json model
        java.srcDirs += 'build/generated/source/db'
    }

    // replace the #const_wiremock_ip# with your current ip address for test with wiremock
    applicationVariants.all { variant ->
        variant.mergeResources.doLast {
            def localIp = obtainCurrentIpAddress()
            File valuesFile = file("${buildDir}/intermediates/res/release/values/values.xml")
            if(valuesFile.exists()) {
                String content = valuesFile.getText('UTF-8')
                content = content.replaceAll("#const_wiremock_ip#", localIp)
                valuesFile.write(content, 'UTF-8')
            }
        }
    }

    // avoid errors with message 'Duplicate files copied in APK ...'
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

apply plugin: 'android-unit-test'
tasks.findByName("assembleDebug").dependsOn("testDebugClasses")

dependencies {

    compile 'com.android.support:support-v4:19.1.+'

    compile project(':GenerateDatabaseClasses')
    compile project(':GenerateJsonRestModel')
    compile("org.springframework.android:spring-android-auth:1.0.1.RELEASE") { exclude module: '*' }
    compile("org.springframework.android:spring-android-core:1.0.1.RELEASE") { exclude module: '*' }
    compile("org.springframework.android:spring-android-rest-template:1.0.1.RELEASE") { exclude module: '*' }

    apt "org.androidannotations:androidannotations:" + androidAnnotationsVersion
    compile("org.androidannotations:androidannotations-api:" + androidAnnotationsAPIVersion )

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile('com.squareup:fest-android:1.0.+') { exclude module: 'support-v4' }
    testCompile ('org.robolectric:robolectric:2.4') {
        exclude module: 'classworlds'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-plugin-registry'
        exclude module: 'maven-profile'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'nekohtml'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'support-v4' // crazy but my android studio don't like this dependency and to fix it remove .idea and re import project
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-http-shared'
        exclude module: 'wagon-provider-api'
    }

    androidTestCompile 'com.google.guava:guava:14.0.1',
            'com.squareup.dagger:dagger:1.1.0',
            'org.hamcrest:hamcrest-integration:1.1',
            'org.hamcrest:hamcrest-core:1.1',
            'org.hamcrest:hamcrest-library:1.1',
            'com.jakewharton.espresso:espresso:1.1-r3'
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        resourcePackageName 'com.example'
    }
}

def obtainCurrentIpAddress() {
    InetAddress inetAddr = InetAddress.getLocalHost();

    byte[] addr = inetAddr.getAddress();
    String ipAddr = "";
    for (int i = 0; i < addr.length; i++) {
        if (i > 0) {
            ipAddr += ".";
        }
        ipAddr += addr[i] & 0xFF;
    }
    ipAddr
}

apply plugin: 'idea'

idea {
    module {
        //and some extra test source dirs
        testSourceDirs += file('src/test/resources')
    }
}

// will be necessary when wiremock is used, to check if running
Process p1 = Runtime.getRuntime().exec("ping -c 1 www.google.com");
int returnVal = p1.waitFor();
boolean reachable = (returnVal==0);
println "Internet is reachable $reachable"

//apply plugin: "java"

//test.reports.html.enabled = false     // just clean up dashboard from not generated reports
//test.reports.junitXml.enabled = false // just clean up dashboard from not generated reports

apply plugin: 'com.github.kt3k.coveralls'

apply from: '../Scripts/jacoco-support.gradle'

apply from: '../Scripts/coveralls-support.gradle'
